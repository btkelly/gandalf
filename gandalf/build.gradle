buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.noveogroup.android:check:1.2.3'
        classpath 'com.btkelly:gnag:0.1.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'gnag-plugin'

def versionString = "1.2.0"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName versionString
    }

    lintOptions {
        abortOnError true
        warning 'InvalidPackage'
    }

    testOptions.unitTests {
        returnDefaultValues = true
        all {
            maxParallelForks = 2
            forkEvery = 150
            afterTest { descriptor, result ->
                println "Executing test for ${descriptor.name} with result: ${result.resultType}"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

gnag {
    gitHubRepoName 'btkelly/gandalf'
    failBuildOnError true
}

check {
    abortOnError false
    checkstyle {
        config project.file('config/checkstyle.xml')
    }

    findbugs {
        config hard()
    }

    pmd {
        config project.file('config/pmd.xml')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.0.1'
    compile 'com.google.code.gson:gson:2.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'gandalf'

    publishedGroupId = 'com.btkelly'

    artifact = 'gandalf'

    libraryName = 'Gandalf'
    libraryDescription = 'Easily notify a user with a simple alert, inform them of an optional update, and in dire situations block the user from accessing older versions of the application completely.'
    libraryVersion = versionString
    libraryIssueTrackerUrl = 'https://github.com/btkelly/gandalf/issues'
    libraryLabels = ['android', 'update']

    licenseName = 'Apache License Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.html'

    gitUrl = 'https://github.com/btkelly/gandalf'
    siteUrl = 'http://btkelly.github.io/gandalf'

    developerId = 'btkelly'
    developerName = 'Bryan Kelly'
    developerEmail = 'bobbake4@gmail.com'
}

apply from: 'https://raw.githubusercontent.com/btkelly/JCenter/master/bintrayv1.gradle'
apply from: 'https://raw.githubusercontent.com/btkelly/JCenter/master/installv1.gradle'