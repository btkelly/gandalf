buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.btkelly:gnag:1.3.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'gnag'

def versionString = "1.3.5"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName versionString
        consumerProguardFiles 'proguard-rules.pro'
    }

    lintOptions {
        abortOnError false
        warning 'InvalidPackage'
    }

    testOptions.unitTests {
        returnDefaultValues = true
        all {
            maxParallelForks = 2
            forkEvery = 150
            afterTest { descriptor, result ->
                println "Executing test for ${descriptor.name} with result: ${result.resultType}"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

gnag {
    checkstyle {
        reporterConfig project.file('config/checkstyle.xml')
    }
    pmd {
        reporterConfig project.file('config/pmd.xml')
    }
    github {
        repoName 'btkelly/gandalf'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.google.code.gson:gson:2.8.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.13'
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'gandalf'

    publishedGroupId = 'com.btkelly'

    artifact = 'gandalf'

    libraryName = 'Gandalf'
    libraryDescription = 'Easily notify a user with a simple alert, inform them of an optional update, and in dire situations block the user from accessing older versions of the application completely.'
    libraryVersion = versionString
    libraryIssueTrackerUrl = 'https://github.com/btkelly/gandalf/issues'
    libraryLabels = ['android', 'update']

    licenseName = 'Apache License Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.html'

    gitUrl = 'https://github.com/btkelly/gandalf'
    siteUrl = 'http://btkelly.github.io/gandalf'

    developerId = 'btkelly'
    developerName = 'Bryan Kelly'
    developerEmail = 'bobbake4@gmail.com'
}

apply from: 'https://raw.githubusercontent.com/btkelly/JCenter/f988f0e106be89c876e038894b9892f9d1d0b643/bintrayv1.gradle'
apply from: 'https://raw.githubusercontent.com/btkelly/JCenter/master/installv1.gradle'
